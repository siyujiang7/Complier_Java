PARSER_BEGIN(SimpleC)
  package simplec.parse;

  import static simplec.AST.*;
  import simplec.Scope;
  import java.util.ListIterator;
  import java.util.*;

  public class SimpleC {
    /** Main entry point. */
    public static void main(String args[]) throws ParseException {
      //TokenizeTest(); //- Lab 1
      //ParseTest();    //- Lab 2
    }

    public static void TokenizeTest() throws ParseException {
      SimpleC parser = new SimpleC(System.in);
      parser.Input();
    }

    public static void ParseTest() {
      try {
        Value.Unit prog = new SimpleC(System.in).goal();
      } catch (Exception e) {
        System.out.println("Parsing related error");
      }
    }
  }


PARSER_END(SimpleC)

  SKIP :
{
    " "
  | "\t"
  | "\n"
  | "\r"
}

SKIP : //comments
{
    < "//" (~["\r", "\n"])* >
  | < "/*" > : ML_COMMENT_STATE
}

<ML_COMMENT_STATE> SKIP :
{
    < "*/" > : DEFAULT
  | < ~[] >   
}

TOKEN :
{
    /* Directives */
    <PRAGMA           : "#pragma" (~["\r", "\n"])* >

    /* Types  */
  | <VOID             : "void">
  | <INT              : "int">
  | <LONG             : "long">
  | <LONG_STAR        : "long*">
  | <CHAR             : "char">
  | <CHAR_STAR        : "char*">
  | <CHAR_STAR_STAR   : "char**">
  | <DOUBLE           : "double">
  | <DOUBLE_STAR      : "double*">
  
    /* Control flow */
  | <IF               : "if">
  | <ELSE             : "else">
  | <DO               : "do">
  | <WHILE            : "while">
  | <FOR              : "for">
  | <CONTINUE         : "continue">
  | <BREAK            : "break">
  | <RETURN           : "return">

    /* Operators */
  | <ADD              : "+">
  | <MIN              : "-">
  | <MUL              : "*">
  | <DIV              : "/">
  | <MOD              : "%">
  | <EQUAL            : "=">
  | <AMP              : "&">
  | <AMPAMP           : "&&">
  | <BARBAR           : "||">
  | <EQUALEQUAL       : "==">
  | <NEQUAL           : "!=">
  | <GEQUAL           : ">=">
  | <LEQUAL           : "<=">
  | <COMMA            : ",">
  | <SEMICOLON        : ";">
  | <LPAREN           : "(">
  | <RPAREN           : ")">
  | <LBRACK           : "[">
  | <RBRACK           : "]">
  | <LCURLY           : "{">
  | <RCURLY           : "}">
  | <LANGLE           : "<">
  | <RANGLE           : ">">

  /* Character string related tokens */
  //| <TYPE             : <ID> "_t">
  | <ID               : <LET> (<LET> | <DEC> | "_")*>
  | <C_CONST          : "'" ( 
      <PRINT>                     /* Regular characters*/
      | "\\" (<OCT>){1,3}         /* Octal character constants */
      | "\\x" (<HEX>){2}          /* Hex character constants */
      | "\\" <PRINT>              /* Escaped characters */
      | "\""                      /* Allow double quotes */
      | "\\" "'"                  /* Allow escaped single quotes */
    ) 
  "'">
  | <STR_CONST        : "\"" (
      <PRINT>             /* Regular characters */
      | ("\\" <PRINT>)    /* Allow escaped regular characters */
      | ("\\" "\"")       /* Allow escaped double quotes */
      | "'"               /* Allow single quotes */
    )* 
  "\"">

  /* Basic regular definitions for characters */
  | <INT_CONST        : (<DEC>)+        /* Decimal or Octal integer constants */
                      | "0x" (<HEX>)+>  /* Hex constants */
  | <DOUBLE_CONST     : (<DEC>)* (".")? (<DEC>)+ (("e"|"E") ("-"|"+")? (<DEC>)+)?>
  | <DEC              : ["0"-"9"]>
  | <PRINT            : <LET> | <DEC> | <OTHER>>
  | <OCT              : ["0"-"7"]>
  | <HEX              : <DEC> | ["A"-"F"] | ["a"-"f"]>
  | <LET              : ["A"-"Z"] | ["a"-"z"]>
  | <OTHER            : " " | "!" | "#" | "$" | "%" | "&" | "(" | ")" | "*" | "+" 
                      | "," | "-" | "." | "/" | ":" | ";" | "<" | "=" | ">" 
                      | "?" | "@" | "[" | "]" | "^" | "_" | "`" | "{" | "|" 
                      | "}" | "~" >
}

/* Root production. */
Value.Unit goal(): 
{
  Value.Unit program;
  Scope.topScope = new Scope(null);
} 
{ 
  program = program() <EOF>
  { 
    return program;
  }
}

Value.Unit program(): 
{
  List<Value> fovList = new LinkedList<Value>();
  Value fov;
} 
{ 
  (fov = function_or_var() { fovList.add(fov); })*
  {
    Value.Unit unit = new Value.Unit(ID("Program"), fovList);
    return unit;
  }
}

Value function_or_var():
{
  Value.Type varType;
  Token id;
  Value fov;
}
{
    varType = var_type() 
    id = <ID> fov = function_or_var_tail(id, varType)
    {
      return fov;
    }
}

Value function_or_var_tail(Token id, Value.Type varType):
{
  Value fov;
}
{
    fov = function(id, varType)   { return fov; }
  | fov = global_var(id, varType) { return fov; }
}

Value.Function function(Token id, Value.Type varType):
{
  Statement.CompoundStatement cStmt; 
  List<Value.Argument> argList;
}
{
  <LPAREN> argList = arguments() <RPAREN> cStmt = compound_statement(argList)
  {
    Value.Function var = new Value.Function(id, varType, argList, cStmt);
    System.err.println("function insert " + var.id.image);
    Scope.topScope.insert(var.id.image, var);
    return var;
  }
}

List<Value.Argument> arguments():
{
  List<Value.Argument> argList = new LinkedList<Value.Argument>();
}
{
  ( argList = arg_list())?
  { return argList; }
}

List<Value.Argument> arg_list():
{
  Value.Argument arg;
  List<Value.Argument> argList = new LinkedList<Value.Argument>();
}
{
  arg = arg() { argList.add(arg); }
  (<COMMA> arg = arg() { argList.add(arg); } )*
  { return argList; }
}

Value.Argument arg():
{
  Token id;
  Value.Type varType;
}
{
  varType = var_type() id = <ID>
  {
    return 
    new Value.Argument(id, varType);
  }
}

Value.VariableList global_var(Token lastVar, Value.Type varType):
{
  List<Value.Variable> vars = new LinkedList<Value.Variable>();
  Value.Variable var = new Value.Variable(lastVar, varType);
  //TODO insert var into topScope*
  Scope.topScope.insert(lastVar.image, var);
  vars.add(var);
  Token globalVarName;
}
{
  ( globalVarName = global_var_list() { 
      var = new Value.Variable(globalVarName, varType);
      //TODO insert var into topScope*
      Scope.topScope.insert(globalVarName.image, var);
      vars.add(var);
    })* <SEMICOLON>
  {
    return new Value.VariableList(vars, true);
  }
}

Token global_var_list(): { Token id; }
{
  <COMMA> id = <ID> { return id; }
}

Value.Type var_type():
{
  Value.Type type;
}
{
    type = var_type_tail(){return type;}
}

Value.Type var_type_tail():
{
  Token type;
}
{
    type = <CHAR>           { return new Value.Type.Type(type); }
  | type = <CHAR_STAR>      { return new Value.Type.Type(type); }
  | type = <CHAR_STAR_STAR> { return new Value.Type.Type(type); }
  | type = <DOUBLE>         { return new Value.Type.Type(type); }
  | type = <DOUBLE_STAR>    { return new Value.Type.Type(type); }
  | type = <INT>            { return new Value.Type.Type(type); }
  | type = <LONG>           { return new Value.Type.Type(type); }
  | type = <LONG_STAR>      { return new Value.Type.Type(type); }
  | type = <VOID>           { return new Value.Type.Type(type); }
}

Statement.AssignStatement assignment(Value.VarAccess var):
{
  Token id;
  Expression expr;
  Expression index;
}
{
    id = <EQUAL> expr = expression()
      { return new Statement.AssignStatement(id, var, expr); }
  | <LBRACK> index = expression() <RBRACK> id = <EQUAL> expr = expression()
      { return new Statement.AssignStatement(id, var, index, expr); }
}

Expression.Call call(Token idName):
{
  List<Expression> args;
}
{
  <LPAREN> args = call_arguments() <RPAREN>
  { return new Expression.Call(idName, args); }
}

List<Expression> call_arg_list():
{
  List<Expression> args = new LinkedList<Expression>();
  Expression expr;
}
{
  expr = expression() { args.add(expr); } (<COMMA> expr = expression() { args.add(expr); })*
  { return args; }
}

List<Expression> call_arguments():
{
  List<Expression> args = new LinkedList<Expression>();
}
{
  (args = call_arg_list())?
  { return args; }
}

Token eql_op(): { Token op; }
{
    op = <EQUALEQUAL> { return op; }
  | op = <NEQUAL> { return op; }
}

Token cmp_op(): { Token op; }
{
    op = <LANGLE> { return op; }
  | op = <RANGLE> { return op; }
  | op = <GEQUAL> { return op; }
  | op = <LEQUAL> { return op; }
}

Token add_op(): { Token op; }
{
    op = <ADD> { return op; }
  | op = <MIN> { return op; }
}

Token mul_op(): { Token op; }
{
    op = <MUL> { return op; }
  | op = <DIV> { return op; }
  | op = <MOD> { return op; }
}

Token unary_op(): { Token op; }
{
    op = <MUL> { return op; }
  | op = <ADD> { return op; }
  | op = <MIN> { return op; }
  | op = <AMP> { return op; }
}

Expression expression():
{
  Expression current;
}
{
  current = logical_or_expr()
  { return current; }
}

// Note that logical_or_expr is how you should correctly implement
// most of the other expr methods. The other methods have been
// left up to you, but are half implemented so that primary_expr()
// will work, and so ints, strings, etc will work out of the box
Expression logical_or_expr():
{
  Expression lhs, rhs, current;
  Token op;
}
{
  lhs = logical_and_expr() { current = lhs; }
  (op = <BARBAR> rhs = logical_or_expr()
  { current = new Expression.Binary.Or(op, current, rhs); })?
  { return current; }
}

Expression logical_and_expr():
{
  Expression lhs, rhs, current;
  Token op;
}
{
  lhs = equality_expr() { current = lhs; }
  (op = <AMPAMP> rhs = logical_and_expr()
  { current = new Expression.Binary.And(op, current, rhs); })?
  { return current; }
}

Expression equality_expr():
{
  Expression lhs, rhs, current;
  Token op;
}
{
  lhs = relational_expr() { current = lhs; }
  (op = eql_op() rhs = equality_expr()
  { current = new Expression.Binary.Eq(op, current, rhs); })?
  { return current; }
}

Expression relational_expr():
{
  Expression lhs, rhs, current;
  Token op;
}
{
  lhs = additive_expr() { current = lhs; }
  (op = cmp_op() rhs = relational_expr()
  { current = new Expression.Binary.Rel(op, current, rhs); })?
  { return current; }
}

Expression additive_expr():
{
  Expression lhs, rhs, current;
  Token op;
}
{
  lhs = multiplicative_expr() { current = lhs; }
  (op = add_op() rhs = additive_expr()
  { current = new Expression.Binary.Add(op, current, rhs); })?
  { return current; }
}

Expression multiplicative_expr():
{
  Expression lhs, rhs, current;
  Token op;
}
{
  lhs = unary_expr() { current = lhs; }
  (op = mul_op() rhs = multiplicative_expr()
  { current = new Expression.Binary.Mul(op, current, rhs); })?
  { return current; }
}

Expression unary_expr():
{
  Expression lhs;
  Token op;
  Expression unary = null;
}
{
    lhs = primary_expr()    { return lhs; }
  | op = unary_op() lhs = unary_expr() 
    { 
      switch(op.image) {
        case "&": unary = new Expression.Unary.Ref(op, lhs); break;
        case "*": unary = new Expression.Unary.Deref(op, lhs); break;
        case "+": unary = new Expression.Unary.Positive(op, lhs); break;
        case "-": unary = new Expression.Unary.Negative(op, lhs); break;
      }
      return unary; 
    }
}

Expression primary_expr():
{
  Token id;
  Expression expr = null;
}
{
    id = <STR_CONST> { return new Expression.Text(id); }
  | id = <C_CONST> { return new Expression.Char(id); }
  | id = <ID> expr = primary_expr_tail(id) { return expr; }
  | id = <INT_CONST>
    { return new Expression.Int(id); }
  | id = <DOUBLE_CONST> 
    { return new Expression.Double(id); }
  | <LPAREN> expr = expression() <RPAREN> { return expr; }
}

Expression primary_expr_tail(Token idName):
{
  Expression expr;
  Token field;
}
{
    <LBRACK> expr = expression() <RBRACK> { return new Expression.Array(idName, expr); }
  | expr = call(idName) { return expr; }
  | {return new Expression.ID(idName); }
}

Statement.CompoundStatement compound_statement(List<Value.Argument> argList):
{
  List<Statement> stmtList;
  Token id;
  Scope newScope = null;
  Scope saved = null;
}
{
  id = <LCURLY> {
    //System.err.println(id.image);
    // TODO, create new scope, save previous scope
    newScope = new Scope(Scope.topScope);
    Scope.topScope = newScope;
    saved = Scope.topScope;
  }
  stmtList = statement_list_opt()
  id = <RCURLY> {
    // TODO: Add arguments into this scope *
    // TODO: revert topScope to saved scope *
    //System.err.println(id.image);
    
    /*
    while(i.hasNext()){
      //System.err.println(i.next().id.image);
      Scope.topScope.insert(i.next().id.image, i.next());
    }
    */
    //Scope.rootScope = Scope.topScope;
    
    if(argList != null){
      ListIterator<Value.Argument> i = argList.listIterator();
    System.err.println("********start add*******");
    for (Value.Argument element : argList) {
      System.err.println("current scop: " + element.id.image);
      Scope.topScope.insert(element.id.image, element);
    }
    System.err.println("********end add*********");
    System.err.println("size: "+ Scope.topScope.symbol_table.size());
    }
    Scope.topScope = saved;
  }

  {
    Scope.topScope = saved; return new Statement.CompoundStatement(id, stmtList, newScope);
  }
}

List<Statement> statement_list_opt():
{
  List<Statement> stmtList = new LinkedList<Statement>();
}
{
  (stmtList = statement_list())?
  { return stmtList; }
}

List<Statement> statement_list():
{
  List<Statement> stmtList = new LinkedList<Statement>();
  Statement stmt;
}
{
  (stmt = statement() { stmtList.add(stmt); } )+
  { return stmtList; }
}

Statement.VariableDecls local_var():
{
  List<Value.Variable> vars;
  Value.Type type;
}
{
  type = var_type() vars = local_var_list(type) <SEMICOLON>
  { 
    for (Value.Variable element : vars) {
      System.err.println("insert var: "+element.id.image);
      Scope.topScope.insert(element.id.image, element);
    }
    return new Statement.VariableDecls(ID("Variable Decls"), vars); 
  }
}

List<Value.Variable> local_var_list(Value.Type varType):
{
  List<Value.Variable> vars = new LinkedList<Value.Variable>();
  Value.Variable var;
  Token id;
}
{
  id = <ID> {
      var = new Value.Variable(id, varType);
      vars.add(var);
    }
  (<COMMA> id = <ID> {
      var = new Value.Variable(id, varType);
      vars.add(var);
    })*
  { return vars; }
}

Statement statement():
{
  Statement stmt;
  Token varName;
  Statement.AssignStatement init;
  Expression cond;
  Statement.AssignStatement update;
  Statement body;
  Statement.ElseStatement elseStmt = null;
  Value.VarAccess varAccess;
  Token id;
}
{   
    stmt = compound_statement(null)
    { return stmt; }
  | varName = <ID> stmt = statement_tail(varName)
    { 
      //System.err.println(varName.image);
      return stmt; 
    }
  | stmt = local_var()
    { return stmt; }
  | id = <IF> <LPAREN> cond = expression() <RPAREN> body = statement() elseStmt = else_optional()
    { return new Statement.IfStatement(id, cond, body, elseStmt); }
  | id = <WHILE> <LPAREN> cond = expression() <RPAREN> body = statement()
    {Statement.WhileStatement sw = new Statement.WhileStatement(id, cond, body);
      System.err.println("loop: "+sw.id.image);
      Scope.topScope.insert(sw.id.image, sw);
      return sw;}
  | id = <DO> body = statement() <WHILE> <LPAREN> cond = expression() <RPAREN> <SEMICOLON>
    {Statement.DoWhileStatement sd = new Statement.DoWhileStatement(id, cond, body);
    	System.err.println("loop: "+sd.id.image);
      Scope.topScope.insert(sd.id.image, sd);
      return sd;}
  | id = <FOR> <LPAREN> varName = <ID> init = assignment(new Value.ScalarAccess(varName)) <SEMICOLON> 
      cond = expression() <SEMICOLON> 
      varName = <ID> update = assignment(new Value.ScalarAccess(varName)) <RPAREN> body = statement()
    {Statement.ForStatement sf = new Statement.ForStatement(id, init, cond, update, body);
      System.err.println("loop: "+sf.id.image);
      Scope.topScope.insert("for", sf);
     return sf;}
  | stmt = jump_statement()
    { 
    	return stmt; 
    }
}

Statement statement_tail(Token varName):
{
  Statement stmt;
  Expression expr;
  Token callTok;
}
{   
    stmt = assignment(new Value.ScalarAccess(varName)) <SEMICOLON>
    { 
      //System.err.println("varname: " + varName.image);
      return stmt; } 
  | expr = call(varName) callTok = <SEMICOLON>
    { return new Statement.CallStatement(callTok, expr); }
}

Statement.ElseStatement else_optional():
{
  Statement stmt = null;
  Statement.ElseStatement elseStmt = null;
  Token elseTok;
}
{
  (LOOKAHEAD(1) elseTok=<ELSE> stmt = statement() 
    {elseStmt = new Statement.ElseStatement(elseTok, stmt);})? 
    {return elseStmt;}
}

Statement jump_statement():
{
  Token jump;
  Expression expr;
}
{
    jump = <CONTINUE> <SEMICOLON>
    { return new Statement.ContinueStatement(jump); }
  | jump = <BREAK> <SEMICOLON>
    { return new Statement.BreakStatement(jump); }
  | jump = <RETURN> expr = return_val_opt() <SEMICOLON>
    { return new Statement.ReturnStatement(jump, expr); }
}

Expression return_val_opt():
{
  Expression expr = null;
}
{
  (expr = expression())?
  { return expr; }
}




// Below this is from lab 1, remove if you want (doesn't matter)
void Input() :
{ int count; }
{
  count=anytoken() <EOF>
  { System.out.println("Total Tokens: " + count); }
}

int anytoken():
{
  Token t;
  int count=0;
}
{
  (
   t = <VOID>       { System.out.println("(VOID, \""+t.image+"\")" );       count++;} |
   t = <LONG>       { System.out.println("(LONE, \""+t.image+"\")" );       count++;} |
   t = <LONG_STAR>    { System.out.println("(LONG_STAR, \""+t.image+"\")" );    count++;} |
   t = <CHAR_STAR>    { System.out.println("(CHAR_STAR, \""+t.image+"\")" );    count++;} |
   t = <CHAR_STAR_STAR> { System.out.println("(CHAR_STAR_STAR, \""+t.image+"\")" ); count++;} |
   t = <DOUBLE>        { System.out.println("(DOUBLE, \""+t.image+"\")" );        count++;} |
   t = <DOUBLE_STAR>     { System.out.println("(DOUBLE_STAR, \""+t.image+"\")" );     count++;} |
   t = <IF>         { System.out.println("(IF, \""+t.image+"\")" );         count++;} |
   t = <ELSE>         { System.out.println("(ELSE, \""+t.image+"\")" );         count++;} |
   t = <DO>         { System.out.println("(DO, \""+t.image+"\")" );         count++;} |
   t = <WHILE>       { System.out.println("(WHILE, \""+t.image+"\")" );       count++;} |
   t = <FOR>        { System.out.println("(FOR, \""+t.image+"\")" );        count++;} |
   t = <CONTINUE>        { System.out.println("(CONTINUE, \""+t.image+"\")" );        count++;} |
   t = <BREAK>        { System.out.println("(BREAK, \""+t.image+"\")" );        count++;} |
   t = <RETURN>        { System.out.println("(RETURN, \""+t.image+"\")" );        count++;} |
   t = <ADD>        { System.out.println("(ADD, \""+t.image+"\")" );        count++;} |
   t = <MIN>        { System.out.println("(MIN, \""+t.image+"\")" );        count++;} |
   t = <MUL>        { System.out.println("(MUL, \""+t.image+"\")" );        count++;} |
   t = <DIV>        { System.out.println("(DIV, \""+t.image+"\")" );        count++;} |
   t = <MOD>        { System.out.println("(MOD, \""+t.image+"\")" );        count++;} |
   t = <EQUAL>        { System.out.println("(EQUAL, \""+t.image+"\")" );        count++;} |
   t = <AMP>        { System.out.println("(AMP, \""+t.image+"\")" );        count++;} |
   t = <AMPAMP>       { System.out.println("(2AMP, \""+t.image+"\")" );       count++;} |
   t = <BARBAR>       { System.out.println("(2BAR, \""+t.image+"\")" );       count++;} |
   t = <EQUALEQUAL>       { System.out.println("(2EQUAL, \""+t.image+"\")" );       count++;} |
   t = <NEQUAL>       { System.out.println("(NEQUAL, \""+t.image+"\")" );       count++;} |
   t = <LEQUAL>       { System.out.println("(LEQUAL, \""+t.image+"\")" );       count++;} |
   t = <GEQUAL>       { System.out.println("(GEQUAL, \""+t.image+"\")" );       count++;} |
   t = <COMMA>       { System.out.println("(COMMA, \""+t.image+"\")" );       count++;} |
   t = <SEMICOLON>       { System.out.println("(SEMICOLON, \""+t.image+"\")" );       count++;} |
   t = <LPAREN>     { System.out.println("(LPAREN, \""+t.image+"\")" );     count++;} |
   t = <RPAREN>     { System.out.println("(RPAREN, \""+t.image+"\")" );     count++;} |
   t = <LBRACK>     { System.out.println("(LBRACK, \""+t.image+"\")" );     count++;} |
   t = <RBRACK>     { System.out.println("(RBRACK, \""+t.image+"\")" );     count++;} |
   t = <LCURLY>     { System.out.println("(LCURLY, \""+t.image+"\")" );     count++;} |
   t = <RCURLY>     { System.out.println("(RCURLY, \""+t.image+"\")" );     count++;} |
   t = <LANGLE>     { System.out.println("(LANGLE, \""+t.image+"\")" );     count++;} |
   t = <RANGLE>     { System.out.println("(RANGLE, \""+t.image+"\")" );     count++;} |
   t = <ID>         { System.out.println("(ID, \""+t.image+"\")" );         count++;} |
   t = <C_CONST>    { System.out.println("(C_CONST, \""+t.image+"\")" );    count++;} |
   t = <DOUBLE_CONST>  { System.out.println("(DOUBLE_CONST, \""+t.image+"\")" );  count++;} | 
   t = <STR_CONST>  { System.out.println("(STR_CONST, \""+t.image+"\")" );  count++;} | 
   t = <INT_CONST>  { System.out.println("(INT_CONST, \""+t.image+"\")" );  count++;} |
   t = <PRINT>      { System.out.println("(PRINT, \""+t.image+"\")" );      count++;} |
   t = <DEC>        { System.out.println("(DEC, \""+t.image+"\")" );        count++;} |
   t = <OCT>        { System.out.println("(OCT, \""+t.image+"\")" );        count++;} |
   t = <HEX>        { System.out.println("(HEX, \""+t.image+"\")" );        count++;} |
   t = <LET>        { System.out.println("(LET, \""+t.image+"\")" );        count++;} |
   t = <OTHER>      { System.out.println("(OTHER, \""+t.image+"\")" );      count++;} |
   t = <PRAGMA>     {}
  )+
  {return count;}
}
